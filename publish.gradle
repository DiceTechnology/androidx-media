// Copyright (C) 2017 The Android Open Source Project
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

apply plugin: 'maven-publish'
apply from: "$gradle.ext.androidxMediaSettingsDir/missing_aar_type_workaround.gradle"

afterEvaluate {
    if (gradle.ext.has('rootProjectIsAndroidXMedia3') && gradle.ext.rootProjectIsAndroidXMedia3) {
        publishing {
            repositories {
                maven {
                    url = System.getenv('AWS_S3_BUCKET_URL')
                    credentials(AwsCredentials) {
                        accessKey = System.getenv('AWS_ACCESS_KEY_ID')
                        secretKey = System.getenv('AWS_SECRET_ACCESS_KEY')
                        sessionToken = System.getenv('AWS_SESSION_TOKEN')
                    }
                }
            }
            publications {
                release(MavenPublication) {
                    from components.release
                    groupId = 'com.endeavorstreaming.androidx-media'
                    artifactId = findProperty('releaseArtifactId') ?: ''
                    version = findProperty('releaseVersion') ?: ''
                    pom {
                        name =
                            findProperty('releaseName')
                            ?: ''
                        description =
                            findProperty('releaseDescription')
                            ?: findProperty('releaseName')
                            ?: ''
                        licenses {
                            license {
                                name = 'The Apache Software License, Version 2.0'
                                url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                                distribution = 'repo'
                            }
                        }
                        developers {
                            developer {
                                name = 'The Android Open Source Project'
                            }
                        }
                        scm {
                            connection = 'scm:git:https://github.com/androidx/media.git'
                            url = 'https://github.com/androidx/media'
                        }
                        withXml {
                            addMissingAarTypeToXml(it)
                        }
                    }
                }
            }
        }
    }
}
tasks.withType(PublishToMavenRepository) {
    if (project.name == 'lib-session' || project.name == 'lib-ui') {
        // Fix the RestrictedApi lint error. 
        // e.g. androidx-media/libraries/session/src/main/java/androidx/media3/session/legacy/MediaBrowserCompat.java:1260
        // Error: ResultReceiver can only be accessed from within the same library group prefix (referenced 
        // groupId=androidx.core with prefix androidx from groupId=com.endeavorstreaming.androidx-media) [RestrictedApi]
        // e.g. androidx-media/libraries/ui/src/main/java/androidx/media3/ui/PlayerNotificationManager.java:1643: 
        // Error: NotificationBuilderWithBuilderAccessor can only be accessed from within the same library group prefix ...
        it.dependsOn test
    } else {
        it.dependsOn lint, test
    }
}
